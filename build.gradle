plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.esaulpaugh'
version '1.4.5-SNAPSHOT'

test {
    maxParallelForks = (int) Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    finalizedBy(javadocJar)
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    finalizedBy(sourcesJar)
}

publishing {
    publications {
        headlong(MavenPublication) {
            artifactId = 'headlong'
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceCompatibility = 8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.madgag.spongycastle:core:1.58.0.0'

//    testImplementation 'org.openjdk.jmh:jmh-core:1.21'
//    testImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.21'
}
