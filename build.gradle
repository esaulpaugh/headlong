import org.gradle.internal.jvm.Jvm

import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.esaulpaugh'
archivesBaseName = "headlong"
version '9.3.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    if (Jvm.current().getJavaVersion() > JavaVersion.VERSION_1_8) {
        options.compilerArgs.addAll(['--release', '8'])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'US-ASCII'
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
}

final String versionStr = getProject().getGradle().getGradleVersion()
final int gradleVersion = Integer.parseInt(versionStr.substring(0, versionStr.indexOf('.')))

task javadocJar(type: Jar, dependsOn: javadoc) {
    if (gradleVersion >= 6) getArchiveClassifier().set("javadoc")
    else classifier = "javadoc"
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    if (gradleVersion >= 6) getArchiveClassifier().set("sources")
    else classifier = "sources"
    from sourceSets.main.allSource
    finalizedBy(javadocJar)
}

final String junitVersion = "5.10.0"
final String jmhVersion = "1.36"
final String bcVersion = "1.75"

static def todayUTC() {
    SimpleDateFormat sdf = new SimpleDateFormat("MMMMM d yyyy")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date())
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': project.name,
                'Build-Date': todayUTC() // DateTimeFormatter.ofPattern("MMMM d yyyy", Locale.ENGLISH).withZone(ZoneId.of("UTC")).format(Instant.now())
        )
    }
    finalizedBy(sourcesJar)
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        headlong(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation("com.google.code.gson:gson:2.10.1")

    test.useJUnitPlatform()
    testImplementation("org.junit.jupiter:junit-jupiter-api:" + junitVersion)
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:" + junitVersion)
    testImplementation("org.bouncycastle:bcprov-jdk14:" + bcVersion)

    jmhImplementation("commons-codec:commons-codec:1.16.0")
    jmhImplementation("org.openjdk.jmh:jmh-core:" + jmhVersion)
    jmhAnnotationProcessor("org.openjdk.jmh:jmh-generator-annprocess:" + jmhVersion)
    jmhImplementation("org.bouncycastle:bcprov-jdk14:" + bcVersion)
}

task jmh(type: JavaExec, dependsOn: jmhClasses) { // run benchmarks with `gradle jmh`
    main = "org.openjdk.jmh.Main" // getMainClass().set("org.openjdk.jmh.Main")
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)

// tasks.withType(AbstractArchiveTask).configureEach {
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}