plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.esaulpaugh'
archivesBaseName = "headlong"
version '2.3.1-SNAPSHOT'

test {
    maxParallelForks = (int) Runtime.runtime.availableProcessors() ?: 1
}

javadoc {
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc' // deprecated
//    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources' // deprecated
//    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    finalizedBy(javadocJar)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    finalizedBy(sourcesJar)
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        headlong(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'

    test.useJUnitPlatform()

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

    testImplementation 'com.madgag.spongycastle:core:1.58.0.0'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.64'


//    implementation 'org.openjdk.jmh:jmh-core:1.23'
//    implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.23'
}