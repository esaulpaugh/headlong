plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.esaulpaugh'
version '1.4.11-SNAPSHOT'

test {
    maxParallelForks = (int) Runtime.runtime.availableProcessors() ?: 1
}

task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc' // deprecated
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
//    classifier = 'sources' // deprecated
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    finalizedBy(javadocJar)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    finalizedBy(sourcesJar)
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

publishing {
    publications {
        headlong(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'

    test.useJUnitPlatform()

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"

    testImplementation 'com.madgag.spongycastle:core:1.58.0.0'

//    implementation 'org.openjdk.jmh:jmh-core:1.22'
//    implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.22'
}

//javadoc {
//    if(JavaVersion.current().isJava9Compatible()) {
//        options.addBooleanOption('html5', true)
//    }
//}
