plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.esaulpaugh'
archivesBaseName = "headlong"
version '3.4.2-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs.addAll(['--release', '8'])
}

test {
    maxParallelForks = (int) Runtime.runtime.availableProcessors() ?: 1
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc' // deprecated
//    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources' // deprecated
//    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    finalizedBy(javadocJar)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': project.name
        )
    }
    finalizedBy(sourcesJar)
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        headlong(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'

    test.useJUnitPlatform()
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.2"

    jmhImplementation 'org.openjdk.jmh:jmh-core:1.23'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.23'
}

task jmh(type: JavaExec, dependsOn: jmhClasses) { // run benchmarks with `gradle jmh`
    main = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)