import java.text.SimpleDateFormat

plugins {
    id("java-library")
    id("maven-publish")
}

group = "com.esaulpaugh"
version = "13.3.2-SNAPSHOT"

final int gradleMajorVersion = gradle.gradleVersion.tokenize('.')[0] as int

System.out.println("gradleMajorVersion = " + gradleMajorVersion)

if (gradleMajorVersion >= 5) {
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
} else {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    if (JavaVersion.current() >= JavaVersion.VERSION_1_10) {
        options.compilerArgs.addAll(["--release", "8"])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    if (gradleMajorVersion >= 6) getArchiveClassifier().set("javadoc")
    else classifier = "javadoc"
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    if (gradleMajorVersion >= 6) getArchiveClassifier().set("sources")
    else classifier = "sources"
    from sourceSets.main.allSource
}

static String todayUTC() {
    SimpleDateFormat sdf = new SimpleDateFormat("MMMM d yyyy")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date())
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Automatic-Module-Name": "com.esaulpaugh.headlong",
                "Created-By": "Gradle",
                "Build-Date": todayUTC()
        )
    }
}

sourceSets {
    jmh {
        java.srcDirs = ["src/jmh/java"]
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

task jmh(type: JavaExec, dependsOn: jmhClasses) { // run benchmarks with `gradle jmh`
    if (gradleMajorVersion >= 7) getMainClass().set("org.openjdk.jmh.Main")
    else main = "org.openjdk.jmh.Main"
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)

assemble.dependsOn(javadocJar, sourcesJar)

repositories {
    mavenCentral()
}

publishing {
    publications {
        headlong(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

configurations {
    gsonJar
    junitApiJar
    junitEngineJar
    junitPlatformLauncherJar
    opentest4jJar
    bcprovJar
}

final String junitVersion = "5.11.4"
final String jmhVersion = "1.37"
final String bcVersion = "1.83"

dependencies {
    implementation("com.google.code.gson:gson:[2.1, 2.13.2]") {
        exclude(group: "com.google.errorprone", module: "error_prone_annotations")
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:" + junitVersion)
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:" + junitVersion)
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.11.4")
    testImplementation("org.bouncycastle:bcprov-jdk14:" + bcVersion)

    jmhImplementation("org.openjdk.jmh:jmh-core:" + jmhVersion)
    jmhAnnotationProcessor("org.openjdk.jmh:jmh-generator-annprocess:" + jmhVersion)
}

dependencies {
    gsonJar("com.google.code.gson:gson:2.13.2") {
        exclude(group: "com.google.errorprone", module: "error_prone_annotations")
    }
    junitApiJar("org.junit.jupiter:junit-jupiter-api:" + junitVersion) {
        transitive = false
    }
    junitEngineJar("org.junit.jupiter:junit-jupiter-engine:" + junitVersion) {
        transitive = false
    }
    junitPlatformLauncherJar("org.junit.platform:junit-platform-launcher:1.11.4") {
        transitive = false
    }
    opentest4jJar("org.opentest4j:opentest4j:1.3.0")
    bcprovJar("org.bouncycastle:bcprov-jdk14:1.83")
}

// Put dependencies into maven local so build.xml can work without "-Dgradle-cache=true"
publishing {
    publications {
        gsonLocal(MavenPublication) {
            groupId = "com.google.code.gson"
            artifactId = "gson"
            version = "2.13.2"
            artifact configurations.gsonJar.singleFile
        }
        junitApiLocal(MavenPublication) {
            groupId = "org.junit.jupiter"
            artifactId = "junit-jupiter-api"
            version = "5.11.4"
            artifact configurations.junitApiJar.singleFile
        }
        junitEngineLocal(MavenPublication) {
            groupId = "org.junit.jupiter"
            artifactId = "junit-jupiter-engine"
            version = "5.11.4"
            artifact configurations.junitEngineJar.singleFile
        }
        junitLauncherLocal(MavenPublication) {
            groupId = "org.junit.platform"
            artifactId = "junit-platform-launcher"
            version = "1.11.4"
            artifact configurations.junitPlatformLauncherJar.singleFile
        }
        opentest4jLocal(MavenPublication) {
            groupId = "org.opentest4j"
            artifactId = "opentest4j"
            version = "1.3.0"
            artifact configurations.opentest4jJar.singleFile
        }
        bcprovLocal(MavenPublication) {
            groupId = "org.bouncycastle"
            artifactId = "bcprov-jdk14"
            version = "1.83"
            artifact configurations.bcprovJar.singleFile
        }
    }
    repositories {
        mavenLocal()
    }
}

build.finalizedBy(
        "publishGsonLocalPublicationToMavenLocal",
        "publishJunitApiLocalPublicationToMavenLocal",
        "publishJunitEngineLocalPublicationToMavenLocal",
        "publishJunitLauncherLocalPublicationToMavenLocal",
        "publishOpentest4jLocalPublicationToMavenLocal",
        "publishBcprovLocalPublicationToMavenLocal"
)

// tasks.withType(AbstractArchiveTask).configureEach {
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}